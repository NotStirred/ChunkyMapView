import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'io.github.notstirred'
version '1.0-SNAPSHOT'

project.ext.lwjglVersion = "3.3.0"
project.ext.jomlVersion = "1.10.3"

ext {
    junitVersion = '4.13.2'
    lombokVersion = "1.18.20"
}

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
                ? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
                : "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        def osArch = System.getProperty("os.arch")
        project.ext.lwjglNatives = osArch.contains("64")
                ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                : "natives-windows-x86"
        break
}

repositories {
    mavenCentral()
    maven {
        url "https://repo.spongepowered.org/repository/maven-public/"
    }

    maven {
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
}

dependencies {

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    implementation "org.spongepowered:noise:2.0.0-SNAPSHOT"
    implementation "com.google.guava:guava:30.1.1-jre"

    implementation "net.daporkchop.lib:unsafe:0.5.5-SNAPSHOT"

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    implementation "org.joml:joml:${jomlVersion}"

    testCompileOnly "junit:junit:$junitVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
}

project.setProperty("mainClassName", "io.github.notstirred.chunkymapview.ChunkyMapView")

jar {
    manifest {
        attributes 'Main-Class': 'io.github.notstirred.chunkymapview.ChunkyMapView'

    }
}

shadowJar {
    manifest {
        inheritFrom project.tasks.jar.manifest
    }
    archiveBaseName.set("$rootProject.name-all")
    archiveClassifier.set('')
    archiveVersion.set('')
}

build.dependsOn "shadowJar"